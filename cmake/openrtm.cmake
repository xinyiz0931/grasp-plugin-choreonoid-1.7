#
# generate_idl_cpp(filenae directory)
#
# Generate C++ stubs from an idl file by omniidl.
#
function(generate_idl_cpp)
  # Parse arguments.
  set(options DISABLE_Wba)
  set(oneValueArgs IDLFILE DESTINATION GENERATED_FILES_VARIABLE)
  set(multiValueArgs)
  cmake_parse_arguments(
    GENERATE_IDL_CPP
    "${options}" "${oneValueArgs}" "${multiValueArgs}"
    ${ARGN}
    )
  # Check required arguments.
  if(NOT DEFINED GENERATE_IDL_CPP_IDLFILE)
    message(FATAL_ERROR "IDLFILE is required")
  else()
    # Get the absolute path of the IDL file.
    get_filename_component(IDLFILE ${GENERATE_IDL_CPP_IDLFILE} ABSOLUTE)
  endif()
  if(NOT DEFINED GENERATE_IDL_CPP_DESTINATION)
    set(DESTINATION ".")
  else()
    set(DESTINATION ${GENERATE_IDL_CPP_DESTINATION})
  endif()

  # Check if rtm-config is installed on the system.
  find_program(RTM_CONFIG rtm-config)
  if(${RTM_CONFIG} STREQUAL RTM_CONFIG-NOTFOUND)
    message(FATAL_ERROR "Unable to find rtm-config")
  endif()

  # Get OpenRTM prefix directory.
  execute_process(
    COMMAND rtm-config --prefix
    OUTPUT_VARIABLE OPENRTM_PREFIX
    OUTPUT_STRIP_TRAILING_WHITESPACE
    )
  # Get executable of omniidl.
  execute_process(
    COMMAND rtm-config --idlc
    OUTPUT_VARIABLE IDLC
    OUTPUT_STRIP_TRAILING_WHITESPACE
    )
  # Get OpenRTM include directory.
  execute_process(
    COMMAND rtm-config --rtm-includedir
    OUTPUT_VARIABLE OPENRTM_INCLUDE_DIRECTORY
    OUTPUT_STRIP_TRAILING_WHITESPACE
    )
  # Get IDL include directory.
  execute_process(
    COMMAND rtm-config --rtm-idldir
    OUTPUT_VARIABLE OPENRTM_IDL_INCLUDE_DIRECTORY
    OUTPUT_STRIP_TRAILING_WHITESPACE
    )
  # Get COIL include directory.
  execute_process(
    COMMAND rtm-config --coil-includedir
    OUTPUT_VARIABLE COIL_INCLUDE_DIRECTORY
    OUTPUT_STRIP_TRAILING_WHITESPACE
    )

  # Get the name of IDL file without extension.
  get_filename_component(_name ${IDLFILE} NAME_WE)

  # Store files to be generated by the omniidl command.
  set(IDL_COMPILED_FILES
    ${DESTINATION}/${_name}.hh
    ${DESTINATION}/${_name}SK.cc
    )

  # Store option flags for omniidl.
  set(IDLFLAGS
    -bcxx -nf -Wbuse_quotes
    -I${OPENRTM_IDL_INCLUDE_DIRECTORY}
    -I${OPENRTM_PREFIX}/include/rtm/idl
    )

  # When DISABLE_Wba is given, exclude generation of code for TypeCode
  # and Any.
  if(NOT GENERATE_IDL_CPP_DISABLE_Wba)
    set(IDLFLAGS ${IDLFLAGS} -Wba)
    set(IDL_COMPILED_FILES ${IDL_COMPILED_FILES} ${DESTINATION}/${_name}DynSK.cc)
  endif()

  # Execute the omniidl command.
  add_custom_command(
    OUTPUT ${IDL_COMPILED_FILES}
    COMMAND ${CMAKE_COMMAND}
    ARGS -E make_directory ${DESTINATION}
    COMMAND ${IDLC}
    ARGS ${IDLFLAGS} -C${DESTINATION} ${IDLFILE}
    MAIN_DEPENDENCY ${IDLFILE}
    COMMENT "Generating C++ stubs for ${_name}"
    )

  # Add include directories to source files properties.
  set(CXX_INCLUDE_FLAGS
    -I${OPENRTM_PREFIX}/include
    -I${COIL_INCLUDE_DIRECTORY}
    -I${OPENRTM_INCLUDE_DIRECTORY}
    -I${OPENRTM_IDL_INCLUDE_DIRECTORY}
    -I${DESTINATION}
    )
  if(${CMAKE_VERSION} VERSION_LESS "3.11.0")
    message(AUTHOR_WARNING
      "Include the directories in compilation flags for files generated by OmniIDL "
      "with the following instruction:"
      "  include_directories(${CXX_INCLUDE_FLAGS})"
      )
  else()
    # INCLUDE_DIRECTORIES property is available since CMake 3.11.
    # https://cmake.org/cmake/help/v3.11/prop_sf/INCLUDE_DIRECTORIES.html
    set_source_files_properties(
      ${IDL_COMPILED_FILES}
      PROPERTIES
      INCLUDE_DIRECTORIES "${CXX_INCLUDE_FLAGS}"
      )
  endif()

  # Set list of generated files to output variable.
  if(DEFINED GENERATE_IDL_CPP_GENERATED_FILES_VARIABLE)
    set(
      ${GENERATE_IDL_CPP_GENERATED_FILES_VARIABLE}
      ${IDL_COMPILED_FILES}
      PARENT_SCOPE
      )
  endif()

endfunction()
